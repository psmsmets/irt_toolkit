! ***************************************************************************
!
!                          Infrasound Ray Tracer PLot
!                                     IRTPL
!
! ***************************************************************************
!
! Program to plot raytrace output generated by IRT3D.
!
! ---
! Pieter Smets (smets@knmi.nl), Anton Van Geyt (anton.van.geyt@knmi.nl)
!
! Seismology Division,
! Royal Netherlands MeteoroLOGICAL Institute (KNMI)
! De Bilt, The Netherlands


! ==================================================================================================
! //////////////////////////////////////////////////////////////////////////////////////////////////
! ==================================================================================================
!
!****************************
PROGRAM IRTPL
!****************************
!  
   USE SYMS
   USE STRING_UTILITY
   USE TOOLS_ATMO
   USE TOOLS_TOPO
   USE TOOLS_NETCDF, ONLY : ISNETCDF
   USE IRTPL_READ
   USE IRTPL_PLOT
   USE EXPORT
   USE MATH
   USE CUBICWEIGHT
!
   IMPLICIT NONE
! 
!.....declare varibles
!
      CHARACTER :: in_inputfile*256, in_atmo*256, in_topo*256, in_reverse*10, out_prefix*256, dummy*256
      CHARACTER :: verb*128, cross_ver*128, re_unit*2, tstr*23
!
      CHARACTER, DIMENSION ( 999 ) :: marker_sym*10
!
      LOGICAL     :: irt, lexist, verbose, jacobian, clipRays 
!
      INTEGER(kind=4) :: i, j, ierr, out_type, azi_cross_ix, io 
      INTEGER(kind=4) :: nofElev, nofAzi, nofRays, skipRays, marker_cnt, hours, minutes
      integer(kind=8) :: epoch
!
      DOUBLE PRECISION :: elevMin, elevMax, elevStep, aziMin, aziMax, aziStep
      DOUBLE PRECISION :: lat0, lon0, h0, h, rk_stepsize, dmax, tmax, omin, omax 
      DOUBLE PRECISION :: cver_bearing, cver_dx, cver_dh, frq
      DOUBLE PRECISION :: t1, t2, time, seconds, clipheight, re_unit_conversion
!
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION ( : )     :: azimuth, elevation
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION ( :, : )  :: angles
!
      DOUBLE PRECISION, DIMENSION ( 999, 6 )          :: marker
!
!  ---
!
      irt = .true.
!
! --------------------------------------------------------------------------------------------------
!
! GET START TIME
!
! --------------------------------------------------------------------------------------------------
!
      CALL CPU_TIME ( t1 )
!
! --------------------------------------------------------------------------------------------------
!
! GET PARAMETERS FROM INPUT FILE
!
! --------------------------------------------------------------------------------------------------
!
      INCLUDE 'irt_argc.f90'
!
! --------------------------------------------------------------------------------------------------
!
! DEFINE TRUE OR FALSE
!
! --------------------------------------------------------------------------------------------------
!
!.....verbose true or false
!
      verbose = ( verb( 1 : 1 ) .EQ. 'v' .OR. verb .EQ. 'y' .OR. verb .EQ. 'yes' )
!
!.....reverse horizontal wind
!
      atmo%lreverse = ( in_reverse .EQ. 'y' .OR. in_reverse .EQ. 'yes' )
!
!.....topography
!
      dummy = StrLowCase ( TRIM ( in_topo ) )
      ltopo = ( dummy .NE. 'n' .AND. dummy .NE. 'no' .AND. dummy .NE. '-' )
!
!.....disable transmission loss calculations
!
      jacobian = .false.
!
! --------------------------------------------------------------------------------------------------
!
! PRINT HEADER
!
! --------------------------------------------------------------------------------------------------
!
      IF ( verbose ) THEN
         PRINT "(A)", ' ______________________________________________________________________________ '
         PRINT "(A)", '|                                                                              |'
         PRINT "(A)", '|                                                                              |'
         PRINT "(A)", '|                       IRTPL - Infrasound Ray Tracer PLot                     |'
         PRINT "(A)", '|                                                                              |'
         PRINT "(A)", '|______________________________________________________________________________|'
         PRINT "(A)", ' '
      ELSE
         PRINT "(A)", 'IRTPL started.'
      ENDIF
!     
! --------------------------------------------------------------------------------------------------
!
! CHECK INPUT
!
! --------------------------------------------------------------------------------------------------
!
      INCLUDE 'irt_check.f90'
!     
! --------------------------------------------------------------------------------------------------
!
! SET NUMBER OFF ...
!
! --------------------------------------------------------------------------------------------------
!
      INCLUDE 'irt_nof.f90'
!
! --------------------------------------------------------------------------------------------------
!
! PRINT PARAMETERS
!
! --------------------------------------------------------------------------------------------------
!
      INCLUDE 'irt_params.f90'
!
! --------------------------------------------------------------------------------------------------
!
! GET ATMOSPHERIC PROFILE AND TOPOGRAPHY
!
! --------------------------------------------------------------------------------------------------
!
      if ( out_type .EQ. 1 ) then
         ltopo = .false.
         call gribtools_get_time( trim(in_atmo), epoch, tstr )
      else
         INCLUDE 'irt_grib.f90'
         tstr=atmo%ranges%tstr
      end if
      tstr(14:14)='H'
      tstr(15:23)=''
!
! --------------------------------------------------------------------------------------------------
!
! SET RANGES
!
! --------------------------------------------------------------------------------------------------
!
      INCLUDE 'irt_ranges.f90'
!
! --------------------------------------------------------------------------------------------------
!
! Get IRT3D output data
!
! --------------------------------------------------------------------------------------------------
!
      IF ( verbose ) PRINT "(A)", 'READ IRT3D OUTPUT DATA'
!
      allocate(  angles(nofRays,2), azimuth(nofAzi), elevation(nofElev) )
!
!.....make list of elevation and azimuth
!
      elevation = (/ ( elevMin + i * elevStep, i = 0, nofElev - 1, 1 ) /)
      azimuth   = (/ ( aziMin  + i * aziStep , i = 0, nofAzi  - 1, 1 ) /)
!
!.....make 2d array of angle combinations
!
      angles( :, 1 ) = (/   ( ( elevMin + i * elevStep ) - &
         & CEILING (  DBLE( i / nofElev )  ) * nofElev * elevStep, &
         & i = 0, nofRays - 1, 1 )  /)
      angles( :, 2 ) = (/  &
         & ( aziMin + CEILING (  DBLE( i / nofElev )  ) * aziStep, &
         & i = 0, nofRays - 1, 1 )  /)
!
!.....read/get reflections
!
      IF ( out_type .EQ. 1 ) CALL READ_REFL (  trim(out_prefix), ierr  )
      IF ( out_type .EQ. 2 ) CALL GET_REFL  (  trim(out_prefix), ierr  )
      IF ( out_type .EQ. 3 ) CALL READ_REFL (  trim(out_prefix), ierr  )
!
      IF ( ierr .GT. 0 ) STOP
!
      IF ( verbose ) PRINT "(A)", ''
!
! --------------------------------------------------------------------------------------------------
!
! Export vertical profile cross-section for specific bearing
!
! --------------------------------------------------------------------------------------------------
!
      IF ( out_type .NE. 1 .AND. cross_ver(1:1) .NE. 'n' .AND. LEN_TRIM (cross_ver) .GT. 5 ) THEN
!
         IF ( verbose ) PRINT "(A)", 'PLOT RAYS FOR BEARING AND VERTICAL ATMOSPHERE CROSS-SECTION'
         IF ( verbose ) PRINT "(3x,A)", 'get parameters for vertical cross-section'
!
!........read parameters
!
         cross_ver = ADJUSTL( cross_ver )
!
         i = SCAN( cross_ver, ' ', .FALSE. )
         READ ( cross_ver( 1 : i ), *, IOSTAT = io ) cver_bearing
         IF ( io .NE. 0 ) cver_bearing = 0.d0
         cross_ver = ADJUSTL(  cross_ver( i + 1 : )  )
!
         i = SCAN( cross_ver, ' ', .FALSE. )
         READ ( cross_ver( 1 : i ), *, IOSTAT = io) cver_dx
         IF ( io .NE. 0 ) cver_dx = 50.d0
         cross_ver = ADJUSTL(  cross_ver( i + 1 : )  )
!
         i = SCAN( cross_ver, ' ', .FALSE. )
         READ ( cross_ver( 1 : i ), *, IOSTAT = io ) cver_dh
         IF ( io .NE. 0 ) cver_dh = .5d0
         cross_ver = ADJUSTL(  cross_ver( i + 1 : )  )
!
         i = SCAN( cross_ver, ' ', .FALSE. )
         READ ( cross_ver( 1 : i ), *, IOSTAT = io ) skipRays
         IF ( io .NE. 0 ) skipRays = 0
         cross_ver = ADJUSTL(  cross_ver( i + 1 : )  )
!
         i = SCAN( cross_ver, ' ', .FALSE. )
         READ ( cross_ver( 1 : i ), *, IOSTAT = io ) clipRays
         IF ( io .NE. 0 ) clipRays = .FALSE.
!
!........check input
!
         IF ( cver_dx .LE. 0.d0 ) THEN
            cver_dx = 50.d0
            PRINT "(A)", 'Warning : horizontal resolution for vertical cross-section can not'
            PRINT "(A)", '          be equal to or smaller than zero! --> hor_res = 50. km.'
         ENDIF
!
         IF ( cver_dh .LE. 0.d0 ) THEN
            cver_dh = .5d0
            PRINT "(A)", 'Warning : vertical resolution for vertical cross-section can not'
            PRINT "(A)", '          be equal to or smaller than zero! --> ver_res = .5 km.'
         ENDIF
!
         IF ( skipRays .LT. 0 ) THEN
            skipRays = 0
            PRINT "(A)", 'Warning : number of rays to skip should be equal to or larger than'
            PRINT "(A)", '          zero! --> skipRays = 0.'
         ENDIF
!
!........Set clip altitude
!
         IF (       clipRays ) clipheight = .97d0 * atmo%hmax
         IF ( .NOT. clipRays ) clipheight = -999.d0 
!
!........find azimuth to plot in vertical cross-section
!
         azi_cross_ix = MINLOC (  DABS( azimuth - cver_bearing ), 1  )
!
!........select markers in range
!
         DO i = 1, marker_cnt
!
            CALL COURSE ( lat0, lon0, marker(i,1), marker(i,2), marker(i,3), marker(i,4) )
            marker(i,4) = cver_bearing - marker(i,4)
            IF (  DABS( marker( i, 4 ) ) .GT. 180.d0  ) &
               & marker(i,4) = SIGN( 1.d0, marker(i,4) ) * ( marker(i,4) - 360.d0 )
            marker(i,5) = marker(i,3) * ( marker(i,4) * D2R )
!
            IF ( DABS( marker(i,4) ) .LT. 1.d0 ) marker(i,6) = 1.d0
!
         ENDDO
!
!........print parameters
!
         IF ( verbose ) THEN
            PRINT "(3x,A,F8.3)", "  o Bearing            (deg) : ", cver_bearing
            PRINT "(3x,A,F8.3)", "  o Azimuth            (deg) : ", azimuth(azi_cross_ix)
            PRINT "(3x,A,F8.3)", "  o Offset             (deg) : ", DABS(azimuth(azi_cross_ix)-cver_bearing)
            PRINT "(3x,A,F8.3)", "  o Horizontal res      (km) : ", cver_dx
            PRINT "(3x,A,F8.3)", "  o Vertical res        (km) : ", cver_dh
            PRINT "(3x,A,I4)",   "  o Skip rays            (-) : ", skipRays
            PRINT "(3x,A,L1)",   "  o Clip rays at top     (-) : ", clipRays
         ENDIF
!
!........read and export wave data for specified azimuth
!
         IF ( out_type .NE. 1 ) THEN
            IF ( verbose ) PRINT "(3x,A)", 'read and export waves for specified azimuth.'
            CALL READ_RAYS ( trim(out_prefix),( azi_cross_ix - 1 ) * nofElev + 1, &
               & azi_cross_ix * nofElev, skipRays, lat0, lon0, rk_stepsize, clipheight, &
               & angles, verbose, ierr )
            IF (ierr .NE. 0) STOP
         ELSE
            OPEN ( UNIT = 40, STATUS = 'replace', FILE = trim(out_prefix) // '_rays_v.xy', FORM = 'formatted' )
            CLOSE ( 40 )
         ENDIF
!
!........export vertical profile c_eff
!
         IF ( verbose ) PRINT "(3x,A)", 'export vertical cross-section for specified bearing.'
         CALL EXPORT_VERTICAL_CROSSECTION (  trim(out_prefix), lat0, lon0, &
            & cver_bearing, cver_dx, cver_dh, dmax  )
!
!........read offset and time for specified azimuth
!
         IF ( verbose ) PRINT "(3x,A)", 'export offset and time of reflection points.'
         CALL EXPORT_OFFSET_TIME (  trim(out_prefix), ( azi_cross_ix - 1 ) * nofElev + 1, &
            & azi_cross_ix * nofElev, cver_bearing, lat0, lon0, tmax, omin, omax  )
!
!........export vertical profile source
!
         IF ( verbose ) PRINT "(3x,A)", 'export atmospheric profile (c and c_eff) at source.'
         CALL EXPORT_PROFILE (  trim(out_prefix) // '_source', lat0, lon0, cver_bearing, cver_dh  )
!
!........make gmt script
!
         IF ( verbose ) PRINT "(3x,A)", 'generate and execute gmt script (' // trim(out_prefix) // '_vert.gmt)'
         CALL MKGMT_VERTICAL (  trim(out_prefix), dmax, atmo%hmax, tmax, omin, omax, cver_dx, cver_dh,&
            & marker( 1 : marker_cnt, : ), marker_sym( 1 : marker_cnt ), re_unit, tstr  )
!
         IF ( verbose ) PRINT "(A)", ''
!
      ELSE
         OPEN ( UNIT = 40, STATUS = 'replace', FILE = trim(out_prefix) // '_rays_h.xy', FORM = 'formatted' )
         CLOSE ( 40 )
      ENDIF
!
! --------------------------------------------------------------------------------------------------
!
! Plot reflection height and travel time
!
! --------------------------------------------------------------------------------------------------
!
      IF ( verbose ) PRINT "(A)", 'PLOT REFLECTION HEIGHTS AND TRAVEL TIMES'
!
!.....export marker symbols
!
      IF ( verbose ) PRINT "(3x,A)", 'export marker symbols'
      CALL EXPORT_MARKERS (  trim(out_prefix), marker( 1 : marker_cnt, : ), marker_sym( 1 : marker_cnt )  ) 
!
!.....export reflection heights and travel times
!
      IF ( verbose ) PRINT "(3x,A)", 'export reflection heights and travel times '
      CALL EXPORT_REFL (  trim(out_prefix)  ) 
!
!.....make and execute gmt script
!
      IF ( verbose ) PRINT "(3x,A)", 'generate and execute gmt script (' // trim(out_prefix) // '_refl.gmt)'
      IF ( atmo%l360 ) THEN
         CALL MKGMT_REFL (  trim(out_prefix), 'sn' ,lat0, lon0, re_unit, tstr  )
      ELSE
         CALL MKGMT_REFL (  trim(out_prefix), 'm' ,lat0, lon0, re_unit, tstr  )
      END IF
!
      IF ( verbose ) PRINT "(A)", ''
!
! -----------------------------------------------------------------------------------------
!
! CLEAN UP
!
! -----------------------------------------------------------------------------------------
!
! 
!.....Deallocate all atmo arrays
!
      CALL ATMO_DEALLOCATE ( 'all' )
      deallocate(angles,azimuth,elevation)
!
! -----------------------------------------------------------------------------------------
!
! GET END TIME
!
! -----------------------------------------------------------------------------------------
!
      CALL CPU_TIME ( t2 )
!
      time = t2 - t1
!
      hours   = FLOOR ( time / 3600.d0 )
      minutes = FLOOR ( time / 60.d0 - hours * 60.d0 )
      seconds = time - hours * 3600.d0 - minutes * 60.d0
!
! -----------------------------------------------------------------------------------------
!
! DISPLAY TERMINATE MESSAGE
!
! -----------------------------------------------------------------------------------------
!   
      IF ( verbose ) THEN
         PRINT "(A,I2,A,I2,A,F6.3,A)", 'IRTPL succesfully terminated. Elapsed time is ', &
            & hours, ' hours ', minutes, ' minutes ', seconds, ' seconds.'
         PRINT "(A)", ''
      ELSE
         PRINT "(A,I2,A,I2,A,F6.3,A)", 'IRTPL succesfully terminated. Elapsed time is ', &
            & hours, ' hours ', minutes, ' minutes ', seconds, ' seconds.'
      ENDIF
!
END PROGRAM IRTPL
!
! =========================================================================================
